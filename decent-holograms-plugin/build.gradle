repositories {
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.13-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'com.github.BeYkeRYkt.LightAPI:lightapi-bukkit-common:5.2.0-Bukkit'
    compileOnly ('com.github.MilkBowl:VaultAPI:1.7') {
        exclude group: 'org.bukkit', module: 'bukkit'
    }
    compileOnly 'com.arcaniax:HeadDatabase-API:13.1'
    compileOnly 'com.github.LoneDev6:api-itemsadder:3.0.0'
    compileOnly 'com.github.oraxen:oraxen:-SNAPSHOT'
    implementation 'dev.dejvokep:boosted-yaml:1.2'
    implementation 'net.kyori:adventure-text-minimessage:4.11.0'
    implementation(project(":decent-holograms-api"))
    implementation(project(":decent-holograms-nms"))
}

processResources {
    def props = [version: rootProject.version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveFileName = "${rootProject.name}-${rootProject.version}.jar"

    relocate 'dev.dejvokep.boostedyaml', 'eu.decentsoftware.holograms.libs.boostedyaml'
    relocate 'net.kyori.adventure.text.minimessage', 'eu.decentsoftware.holograms.libs.minimessage'
}

assemble.dependsOn shadowJar